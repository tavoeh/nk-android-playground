// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        classpath "com.autonomousapps:dependency-analysis-gradle-plugin:0.79.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

apply plugin: "com.autonomousapps.dependency-analysis"

// usage: ./gradlew buildHealth
dependencyAnalysis {
    issues {
        all {
            ignoreKtx(true) // default is false
        }
    }
}

subprojects { subProject ->
    afterEvaluate {
        if (subProject.plugins.hasPlugin("kotlin") && subProject.plugins.hasPlugin("java-library")) {
            subProject.kotlin.copyClassesToJavaOutput = true
            subProject.jar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        }
    }
    //this allows to run ktlint for a specific module
    // example usage: ./gradlew :data:ktlintFormat
    apply from: "$project.rootDir/kotlin-code-quality.gradle"
}


task clean(type: Delete) {
    delete rootProject.buildDir
}